import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.70"
    id "org.jetbrains.dokka" version "0.10.1" apply false
    id "com.github.johnrengelman.shadow" version "5.2.0" apply false
}

allprojects {
    repositories {
        mavenCentral()
        maven {
            name "jitpack.io"
            url "https://jitpack.io"
        }
        maven {
            name "jcenter"
            url "https://jcenter.bintray.com"
        }
    }

    tasks.withType(KotlinCompile) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()

        afterTest { TestDescriptor descriptor, TestResult result ->
            def text = "[TEST] ${result.resultType} :: ${descriptor.className} ${descriptor.displayName}"

            logger.quiet text
        }

        afterSuite { TestDescriptor descriptor, TestResult result ->
            def text = "[SUITE] [${result.resultType}] (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"

            if (!descriptor.parent) {
                logger.quiet text
            }
        }
    }
}


